import org.arquillian.spacelift.execution.Tasks
import org.arquillian.spacelift.gradle.*
import org.arquillian.spacelift.gradle.maven.*
import org.arquillian.spacelift.gradle.utils.*
import org.arquillian.spacelift.process.CommandBuilder
import org.arquillian.spacelift.process.impl.CommandTool

defaultTasks 'test'
apply plugin: 'spacelift'

ext {
    defaultArquillianCoreVersion = ['1.1.4.Final', '1.1.5.Final', '1.1.6.Final', '1.1.7.Final', '1.1.8.Final',
                                    '1.1.9.Final', '1.1.10.Final', '1.1.11.Final']
    defaultBrowser = ['phantomjs']

    defaultArquillianPhantomDriverCommit = '1.2.1.Final'
    defaultArquillianPhantomDriverOrganization = 'qa'

    // selenium server instance holder
    seleniumVersion = '2.52.0'
    iedriverVersion = '2.52.0'
    phantomjsVersion = '2.1.1'
}

spacelift {
    workspace = new File(project.rootDir, 'ws')
    installationsDir = new File(project.rootDir, 'ws')
    enableStaging = true
    tools {
       java {
            command ([
                linux: "java", 
                windows: "java.exe", 
                mac: "java",
                solaris: "java"
            ])
       }
       mvn {
            command {
                def m2 = System.getenv("M2")
                def m2_home = System.getenv("M2_HOME")

                def mvnExpFile = 'mvn'
                if(EnvironmentUtils.runsOnWindows()) {
                    mvnExpFile += '.bat'
                }

                def mvnPath = null
                if (m2 != null && !m2.isEmpty() && (new File(m2, mvnExpFile)).exists()) {
                    mvnPath = new File(new File(m2), mvnExpFile).getCanonicalPath()
                } else if (m2_home != null && !m2_home.isEmpty() && (new File(m2_home, mvnExpFile)).exists()) {
                    mvnPath = new File(new File(m2_home), mvnExpFile).getCanonicalPath()
                } else {
                    mvnPath = mvnExpFile
                }

                

                return Tasks.prepare(CommandTool).command(new CommandBuilder(mvnPath))
            }
        }
    }
    profiles {
        "default" {
            enabledInstallations 'selenium', 'phantomjs', 'iedriver'
            tests '*'            
        }
    }
    installations {
        selenium {
            product 'selenium'
            version project.seleniumVersion
            remoteUrl "http://selenium-release.storage.googleapis.com/${version.substring(0,4)}/selenium-server-standalone-${version}.jar"
            autoExtract false           
            tools {
                seleniumserver {
                    command {
                        GradleSpacelift.tools('java')
                            .parameters('-jar', "${selenium.home}/${selenium.fileName}")
                    }
                }
            }
        }
        phantomjs {
            product 'phantomjs'
            version project.phantomjsVersion
            remoteUrl ([
                linux: "https://bitbucket.org/ariya/phantomjs/downloads/phantomjs-${version}-linux-x86_64.tar.bz2",
                mac: "https://bitbucket.org/ariya/phantomjs/downloads/phantomjs-${version}-macosx.zip",
                windows: "https://bitbucket.org/ariya/phantomjs/downloads/phantomjs-${version}-windows.zip",
            ])
            home "phantomjs-${version}"
            extractMapper { toDir(home).cutdirs() }
            postActions {
                // fix executable flags
                project.ant.chmod(dir: "${home}", perm:"a+x", includes:"**/*phantomjs*")
            }
        }
        iedriver {
            product 'iedriver'
            version project.iedriverVersion
            remoteUrl "http://selenium-release.storage.googleapis.com/${version.substring(0,4)}/IEDriverServer_x64_${version}.zip"
            home "iedriver-${version}"
            extractMapper { toDir(home).cutdirs() }
        }

        // this installation can be used if there is a need to test against not yet release version of arquillian-phantom-driver
        arquillianPhantomDriver {
            product 'arquillianPhantomDriver'
            version { project.arquillianPhantomDriverCommit }
            remoteUrl { "https://github.com/${project.arquillianPhantomDriverOrganization}/arquillian-phantom-driver/archive/${version}.zip" }
            home { "arquillian-phantom-driver-${version}" }
            extractMapper { toDir(home).cutdirs() }
            postActions {
                Tasks.prepare(MavenExecutor).pom("${home.absolutePath}/pom.xml")
                    .goals('clean', 'install')
                    .execute().await()
            }
        }
    }
    tests {
        arquillianCoreCompatibility {

            def seleniumServer = null

            dataProvider {
                def browser = project.browser
                project.arquillianCoreVersion.collectMany{arqVersion ->
                    browser.collect{ b->
                        [arqVersion, b]
                    }
                }
            }
            beforeSuite {

                def phantomjsPath = "${phantomjs.home}/bin/phantomjs"
                if(EnvironmentUtils.runsOnWindows()) {
                    phantomjsPath += '.exe'
                }
                

                // start selenium server, we need it for tests
                seleniumServer = GradleSpacelift.tools('seleniumserver')
                    .workingDir("${project.spacelift.workspace}")
                    .parameter("-Dphantomjs.binary.path=${phantomjsPath}")
                    .parameter("-Dwebdriver.ie.driver=${iedriver.home}/IEDriverServer.exe")
                    .interaction(GradleSpacelift.ECHO_OUTPUT)
                    .execute()
            }
            execute { arqVersion, browserType ->
                // execute all tests in the extension
                Tasks.prepare(MavenExecutor).pom("pom.xml")
                    // replace property version.arquillian.core with compatibility version
                    .property("version.arquillian.core=${arqVersion}")
                    .property("browser=${browserType}")
                    .goals('clean', 'verify').execute().await()
            }
            afterSuite {
                // terminate selenium server
                seleniumServer.terminate()
            }
        }
    }
}

// build dependencies
buildscript {
    repositories {
        mavenCentral()
        // you need this only if you plan to use SNAPSHOT version of this plugin
        maven {
            name 'jboss-staging-repository-group'
            url 'https://repository.jboss.org/nexus/content/groups/staging'
        }
    }
    dependencies {
        classpath 'org.arquillian.spacelift.gradle:arquillian-spacelift-gradle:1.0.0-alpha-5'
    }
}

// these elements are needed only if you are willing to use _testreport_ task
repositories {
    mavenCentral()
}

configurations {
    junitreport
}

dependencies {
    junitreport 'org.apache.ant:ant-junit:1.9.4'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}
